managed;
//implementation in class zbp_i_travel_bagga_m unique;
strict ( 2 );

define behavior for ZI_TRAVEL_BAGGA_M //alias Travel
implementation in class zcl_bp_travel_bagga_m unique
persistent table ztravel_bagga_m
lock master
authorization master ( instance )
etag master LastChangedAt
early numbering
{

  create ( authorization : global );
  update;
  delete;
  field ( readonly ) TravelId, CreatedAt, CreatedBy, LastChangedBy, LastChangedAt;
  field ( mandatory ) AgencyId, CustomerId, BeginDate, EndDate, OverallStatus, BookingFee, CurrencyCode;
  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;
  //////////  factory action is called when we have to copy the complete row of the business object and crete the new one
  factory action copyTravel [1];
  //////  internal action is called by determination which is triggered on some field change
  internal action recalcTotPrice;
  validation validateCustomer on save { create; field CustomerId; }
  validation validateDates on save {create; field BeginDate, EndDate; }
  validation validateCurrencyCode on save { create; field CurrencyCode; }
  validation validateBookingFee on save { create; field BookingFee; }
  validation validateStatus on save { create; field OverallStatus; }

//////We have added the features:instance so that we can control the assocation will be available to user or not based on condition
  association _Booking { create ( features : instance ); }

  mapping for ztravel_bagga_m
    {
      TravelId      = travel_id;
      AgencyId      = agency_id;
      CustomerId    = customer_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      BookingFee    = booking_fee;
      TotalPrice    = total_price;
      CurrencyCode  = currency_code;
      Description   = description;
      OverallStatus = overall_status;
      CreatedBy     = created_by;
      CreatedAt     = created_at;
      LastChangedBy = last_changed_by;
      LastChangedAt = last_changed_at;
    }
}

define behavior for ZI_BOOKING_BAGGA_M //alias <alias_name>
implementation in class zcl_bp_booking_bagga_m unique
persistent table zbooking_bagga_m
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, LastChangedAt;

  field ( mandatory ) CarrierId, ConnectionId, FlightDate, BookingStatus;
  field ( mandatory : create, readonly : update ) BookingDate, CustomerId;
  validation validateStatus on save { create; field BookingStatus; }
  validation validateCustomer on save {create; field CustomerId; }
  validation validateCurrencyCode on save {create; field CurrencyCode; }
  validation validateConnection on save {create; field CarrierId, ConnectionId,FlightDate; }
  validation  validateFlightPrice on save { create; field FlightPrice; }

  association _Travel;
  association _Bookingsuppl { create ( features : instance ); }

  mapping for zbooking_bagga_m
    {
      TravelId      = travel_id;
      BookingId     = booking_id;
      BookingDate   = booking_date;
      CustomerId    = customer_id;
      CarrierId     = carrier_id;
      ConnectionId  = connection_id;
      FlightDate    = flight_date;
      FlightPrice   = flight_price;
      CurrencyCode  = currency_code;
      BookingStatus = booking_status;
      LastChangedAt = last_changed_at;
    }
}

define behavior for ZI_BOOKSUPPL_BAGGA_M //alias <alias_name>
implementation in class zcl_bp_booksuppl_bagga_m unique
persistent table zbooksuppl_bagga
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, BookingSupplementId, LastChangedAt;
  field ( mandatory ) Price, SupplementId;
  validation  validateCurrencyCode on save{ create; field CurrencyCode; }
  validation validateSupplement on save {create; field SupplementId; }
  validation valiatePrice on save { create; field Price; }
  association _Travel;
  association _Booking;
  mapping for zbooksuppl_bagga
    {
      TravelId            = travel_id;
      BookingId           = booking_id;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LastChangedAt       = last_changed_at;
    }

}